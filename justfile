set dotenv-load := false

name := 'cosmic-applet-clippy-land'
appid := 'com.keewee.CosmicAppletClippyLand'

# configurable paths
bin_dir := env_var_or_default("BIN_DIR", "~/.local/bin")
app_dir := env_var_or_default("APP_DIR", "~/.local/share/applications")
icon_dir := env_var_or_default("ICON_DIR", "~/.local/share/icons/hicolor/scalable/apps")
metainfo_dir := env_var_or_default("METAINFO_DIR", "~/.local/share/metainfo")

# default recipe
_default:
    @just --list

# Build release binary
build:
    cargo build --release

# Install for current user
install: build
    install -Dm755 target/release/{{name}} {{bin_dir}}/{{name}}
    install -Dm644 resources/com.keewee.CosmicAppletClippyLand.desktop {{app_dir}}/{{appid}}.desktop
    install -Dm644 resources/app.metainfo.xml {{metainfo_dir}}/{{appid}}.metainfo.xml
    install -Dm644 resources/icon.svg {{icon_dir}}/{{appid}}.svg
    update-desktop-database {{app_dir}} || true
    gtk-update-icon-cache -f ~/.local/share/icons/hicolor || true

# Uninstall for current user
uninstall:
    rm -f {{bin_dir}}/{{name}}
    rm -f {{app_dir}}/{{appid}}.desktop
    rm -f {{metainfo_dir}}/{{appid}}.metainfo.xml
    rm -f {{icon_dir}}/{{appid}}.svg
    update-desktop-database {{app_dir}} || true
    gtk-update-icon-cache -f ~/.local/share/icons/hicolor || true

# Clean build artifacts
clean:
    cargo clean
